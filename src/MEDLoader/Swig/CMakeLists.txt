# Copyright (C) 2012-2026  CEA, EDF
#
# This library is free software; you can redistribute it and/or modify it under
# the terms of the GNU Lesser General Public License as published by the Free
# Software Foundation; either version 2.1 of the License, or (at your option)
# any later version.
#
# This library is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for more
# details.
#
# You should have received a copy of the GNU Lesser General Public License along
# with this library; if not, write to the Free Software Foundation, Inc., 59
# Temple Place, Suite 330, Boston, MA  02111-1307 USA
#
# See http://www.salome-platform.org/ or email :
# webmaster.salome@opencascade.com
#
# Author : Anthony Geay (CEA/DEN)

find_package(SWIG REQUIRED)
include(${SWIG_USE_FILE})

if(MEDCOUPLING_BUILD_PY_TESTS)
  include(tests.set)
endif()

add_definitions(${PYTHON_DEFINITIONS} ${NUMPY_DEFINITIONS} ${SCIPY_DEFINITIONS})

set_source_files_properties(MEDLoader.i PROPERTIES CPLUSPLUS ON)
if("${PYTHON_VERSION_MAJOR}" STREQUAL "3")
  set_source_files_properties(MEDLoader.i PROPERTIES SWIG_FLAGS "-py3")
else()
  set_source_files_properties(MEDLoader.i PROPERTIES SWIG_DEFINITIONS "-shadow")
endif()
set(SWIG_MODULE_MEDLoader_EXTRA_FLAGS
    "${NUMPY_DEFINITIONS};${SCIPY_DEFINITIONS}")
if(MEDCOUPLING_USE_64BIT_IDS)
  string(APPEND SWIG_MODULE_MEDLoader_EXTRA_FLAGS
         ";-DMEDCOUPLING_USE_64BIT_IDS")
endif(MEDCOUPLING_USE_64BIT_IDS)

set(MEDLoader_SWIG_DPYS_FILES MEDLoaderCommon.i MEDLoaderTypemaps.i)

include_directories(
  ${NUMPY_INCLUDE_DIR}
  ${PYTHON_INCLUDE_DIRS}
  ${MEDFILE_INCLUDE_DIRS}
  ${HDF5_INCLUDE_DIRS}
  ${CMAKE_CURRENT_SOURCE_DIR}
  ${CMAKE_CURRENT_SOURCE_DIR}/..
  ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDCoupling_Swig
  ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDCoupling
  ${CMAKE_CURRENT_SOURCE_DIR}/../../INTERP_KERNEL
  ${CMAKE_CURRENT_SOURCE_DIR}/../../INTERP_KERNEL/Bases
  ${CMAKE_CURRENT_SOURCE_DIR}/../../INTERP_KERNEL/Geometric2D
  ${CMAKE_CURRENT_SOURCE_DIR}/../../INTERP_KERNEL/ExprEval
  ${CMAKE_CURRENT_SOURCE_DIR}/../../INTERP_KERNEL/GaussPoints
  ${PROJECT_BINARY_DIR}/doc)

set(SWIG_MODULE_MEDLoader_EXTRA_DEPS
    ${MEDLoader_SWIG_DPYS_FILES} ${medloader_HEADERS_HXX}
    ${medcoupling_HEADERS_HXX} ${medcoupling_HEADERS_TXX}
    ${interpkernel_HEADERS_HXX} ${interpkernel_HEADERS_TXX})

# SWIG must run after the doc if we want to have the docstrings extracted from
# Doxygen into the Python module:
if(MEDCOUPLING_BUILD_DOC)
  list(APPEND SWIG_MODULE_MEDLoader_EXTRA_FLAGS -DWITH_DOCSTRINGS)
  list(APPEND SWIG_MODULE_MEDLoader_EXTRA_DEPS
       ${PROJECT_BINARY_DIR}/doc/MEDLoader_doc.i swig_ready)
endif()

if(WIN32)
  set_property(SOURCE MEDLoader.i PROPERTY COMPILE_DEFINITIONS WIN32)
endif()

if(${CMAKE_VERSION} VERSION_LESS "3.8.0")
  swig_add_module(MEDLoader python MEDLoader.i)
else()
  swig_add_library(
    MEDLoader
    LANGUAGE python
    SOURCES MEDLoader.i)
endif()

swig_link_libraries(MEDLoader ${PLATFORM_LIBS} medloader medcouplingcpp)
if(WIN32 OR CYGWIN)
  # PYTHON_LIBRARIES only needed on win32 see
  # https://www.python.org/dev/peps/pep-0513/#libpythonx-y-so-1
  swig_link_libraries(MEDLoader ${PYTHON_LIBRARIES})
endif()

swig_check_generation(MEDLoader)
if(WIN32 AND NOT MINGW)
  set_target_properties(_MEDLoader PROPERTIES DEBUG_OUTPUT_NAME _MEDLoader_d)
  # To increase the size of the .obj file on Windows because
  # MEDLoaderPYTHON_wrap.cxx, generated by SWIG, is too big
  target_compile_options(_MEDLoader PRIVATE /bigobj)
endif(WIN32 AND NOT MINGW)

install(TARGETS _MEDLoader DESTINATION ${MEDCOUPLING_INSTALL_PYTHON})
install(FILES MEDLoader.i MEDLoaderTypemaps.i MEDLoaderCommon.i
              MEDLoaderFinalize.i DESTINATION ${MEDCOUPLING_INSTALL_HEADERS})

salome_install_scripts(
  ${CMAKE_CURRENT_BINARY_DIR}/MEDLoader.py ${MEDCOUPLING_INSTALL_PYTHON}
  EXTRA_DPYS "${SWIG_MODULE_MEDLoader_REAL_NAME}")

install(FILES MEDLoaderSplitter.py MEDLoaderFinalize.py
        DESTINATION ${MEDCOUPLING_INSTALL_PYTHON})

install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDConverter/abaqus.py
              ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDConverter/ansys.py
              ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDConverter/aster.py
              ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDConverter/cells.py
              ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDConverter/connectivity.py
              ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDConverter/logger.py
              ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDConverter/MEDConverter.py
              ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDConverter/MEDConverterMesh.py
              ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDConverter/systus.py
              ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDConverter/tetgen.py
              ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDConverter/utilities.py
              ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDConverter/zset.py
              ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDConverter/__init__.py
        DESTINATION ${MEDCOUPLING_INSTALL_PYTHON}/MEDConverter)

install(
  FILES ${ALL_TESTS}
        MEDLoaderDataForTest.py
        MEDLoaderTest1.py
        MEDLoaderTest2.py
        MEDLoaderTest3.py
        CrackAlongTest.py
        MEDConverterTest.py
  DESTINATION ${MEDCOUPLING_INSTALL_SCRIPT_SCRIPTS})

install(FILES CaseIO.py CaseReader.py CaseWriter.py VTKReader.py
        DESTINATION ${MEDCOUPLING_INSTALL_PYTHON})

install(
  FILES case2med
  PERMISSIONS
    OWNER_EXECUTE
    OWNER_WRITE
    OWNER_READ
    GROUP_EXECUTE
    GROUP_READ
    WORLD_EXECUTE
    WORLD_READ
  DESTINATION ${MEDCOUPLING_INSTALL_BINS})
install(
  FILES med2case
  PERMISSIONS
    OWNER_EXECUTE
    OWNER_WRITE
    OWNER_READ
    GROUP_EXECUTE
    GROUP_READ
    WORLD_EXECUTE
    WORLD_READ
  DESTINATION ${MEDCOUPLING_INSTALL_BINS})
install(
  FILES ConvertMEDFileTo33.py ConvertMEDFileTo30.py
  PERMISSIONS
    OWNER_EXECUTE
    OWNER_WRITE
    OWNER_READ
    GROUP_EXECUTE
    GROUP_READ
    WORLD_EXECUTE
    WORLD_READ
  DESTINATION ${MEDCOUPLING_INSTALL_BINS})

salome_generate_tests_environment(tests_env)

foreach(test ${BASE_TESTS})
  get_filename_component(testname ${test} NAME_WE)
  add_test(NAME ${testname} COMMAND ${PYTHON_EXECUTABLE}
                                    ${CMAKE_CURRENT_SOURCE_DIR}/${test})
  set_tests_properties(${testname} PROPERTIES ENVIRONMENT "${tests_env}")
endforeach()

if(NUMPY_FOUND)
  # Add Remapper to the Python path:
  salome_accumulate_environment(
    PYTHONPATH NOCHECK "${CMAKE_CURRENT_BINARY_DIR}/../../MEDCoupling_Swig")
  salome_generate_tests_environment(tests_env)
  foreach(test ${NUMPY_TESTS})
    get_filename_component(testname ${test} NAME_WE)
    add_test(NAME ${testname} COMMAND ${PYTHON_EXECUTABLE}
                                      ${CMAKE_CURRENT_SOURCE_DIR}/${test})
    set_tests_properties(${testname} PROPERTIES ENVIRONMENT "${tests_env}")
  endforeach()
endif(NUMPY_FOUND)

# Application tests

set(TEST_INSTALL_DIRECTORY ${MEDCOUPLING_INSTALL_TESTS}/MEDLoader_Swig)

set(MEDLOADER_TEST_FILES
    ${ALL_TESTS}
    MEDLoaderDataForTest.py
    MEDLoaderTest1.py
    MEDLoaderTest2.py
    MEDLoaderTest3.py
    CrackAlongTest.py
    MEDConverterTest.py
    FilesForTest/quad9.mail
    FilesForTest/sslp200b.mail
    FilesForTest/WrongGrpName.mail
    FilesForTest/ABAQUS_MIXT_ELEMENT.inp
    FilesForTest/ANSYS_Modele_FE_Mixte_v4.cdb
    FilesForTest/SYSTUS_MULTI_DONN1.ASC
    FilesForTest/ZSET_MULTI.geof
    CaseIO.py
    CaseReader.py
    CaseWriter.py
    VTKReader.py
    MEDLoaderSplitter.py)

install(FILES ${MEDLOADER_TEST_FILES} DESTINATION ${TEST_INSTALL_DIRECTORY})

install(
  FILES CTestTestfileInstall.cmake
  DESTINATION ${TEST_INSTALL_DIRECTORY}
  RENAME CTestTestfile.cmake)
install(FILES tests.set DESTINATION ${TEST_INSTALL_DIRECTORY})
