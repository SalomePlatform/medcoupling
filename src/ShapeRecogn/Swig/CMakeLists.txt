# Copyright (C) 2024-2026  CEA, EDF
#
# This library is free software; you can redistribute it and/or modify it under
# the terms of the GNU Lesser General Public License as published by the Free
# Software Foundation; either version 2.1 of the License, or (at your option)
# any later version.
#
# This library is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for more
# details.
#
# You should have received a copy of the GNU Lesser General Public License along
# with this library; if not, write to the Free Software Foundation, Inc., 59
# Temple Place, Suite 330, Boston, MA  02111-1307 USA
#
# See http://www.salome-platform.org/ or email :
# webmaster.salome@opencascade.com
#

find_package(SWIG REQUIRED)
include(${SWIG_USE_FILE})

add_definitions(${PYTHON_DEFINITIONS})

set_source_files_properties(ShapeRecogn.i PROPERTIES CPLUSPLUS ON)
if("${PYTHON_VERSION_MAJOR}" STREQUAL "3")
  set_source_files_properties(ShapeRecogn.i PROPERTIES SWIG_FLAGS "-py3")
else()
  set_source_files_properties(ShapeRecogn.i PROPERTIES SWIG_DEFINITIONS
                                                       "-shadow")
endif()

set(SWIG_MODULE_ShapeRecogn_EXTRA_FLAGS "")
if(MEDCOUPLING_USE_64BIT_IDS)
  string(APPEND SWIG_MODULE_ShapeRecogn_EXTRA_FLAGS
         "-DMEDCOUPLING_USE_64BIT_IDS")
endif(MEDCOUPLING_USE_64BIT_IDS)

set(ShapeRecogn_SWIG_DPYS_FILES ShapeRecogn.i)

include_directories(
  ${PYTHON_INCLUDE_DIRS}
  ${MEDFILE_INCLUDE_DIRS}
  ${HDF5_INCLUDE_DIRS}
  ${CMAKE_CURRENT_SOURCE_DIR}
  ${CMAKE_CURRENT_SOURCE_DIR}/..
  ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDLoader
  ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDCoupling_Swig
  ${CMAKE_CURRENT_SOURCE_DIR}/../../MEDCoupling
  ${CMAKE_CURRENT_SOURCE_DIR}/../../INTERP_KERNEL
  ${CMAKE_CURRENT_SOURCE_DIR}/../../INTERP_KERNEL/Bases)

set(SWIG_MODULE_ShapeRecogn_EXTRA_DEPS
    ${ShapeRecogn_SWIG_DPYS_FILES} ${shaperecogn_HEADERS_HXX}
    ${medcoupling_HEADERS_HXX} ${medcoupling_HEADERS_TXX}
    ${interpkernel_HEADERS_HXX} ${interpkernel_HEADERS_TXX})

if(WIN32)
  set_property(SOURCE ShapeRecogn.i PROPERTY COMPILE_DEFINITIONS WIN32)
endif()

if(${CMAKE_VERSION} VERSION_LESS "3.8.0")
  swig_add_module(ShapeRecogn python ShapeRecogn.i)
else()
  swig_add_library(
    ShapeRecogn
    LANGUAGE python
    SOURCES ShapeRecogn.i)
endif()

swig_link_libraries(ShapeRecogn ${PYTHON_LIBRARIES} ${PLATFORM_LIBS}
                    shaperecogn medloader medcouplingcpp)
swig_check_generation(ShapeRecogn)
if(WIN32 AND NOT MINGW)
  set_target_properties(_ShapeRecogn PROPERTIES DEBUG_OUTPUT_NAME
                                                _ShapeRecogn_d)
  # To increase the size of the .obj file on Windows because
  # ShapeRecognPYTHON_wrap.cxx, generated by SWIG, is too big
  target_compile_options(_ShapeRecogn PRIVATE /bigobj)
endif(WIN32 AND NOT MINGW)

install(TARGETS _ShapeRecogn DESTINATION ${MEDCOUPLING_INSTALL_PYTHON})

salome_install_scripts(
  ${CMAKE_CURRENT_BINARY_DIR}/ShapeRecogn.py ${MEDCOUPLING_INSTALL_PYTHON}
  EXTRA_DPYS "${SWIG_MODULE_ShapeRecogn_REAL_NAME}")
